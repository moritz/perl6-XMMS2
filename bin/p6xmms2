#!/usr/bin/env perl6
use v6;
use XMMS2::Client;

#= Gets all the param-less methods - no new() or BUILD().
#  This is 2 and not 0 because methods have an implied :(self, *%_).
my @commands = XMMS2::Client.^methods(:local).grep(*.signature.params == 2);

multi sub MAIN(Str $command) {
    # I already tried doing this in the signature, but when it doesn't match Rakudo barfs.
    unless $command ~~ any @commands {
        say "'$command'?";
        USAGE;
        exit 1;
    }

    XMMS2::Client.new(:client-name<p6xmms2>)."$command"();
}

sub USAGE {
    say "Usage: $*PROGRAM_NAME <command>";
    say;
    say "Available commands:";
    say "    $_" for @commands;
}
